/mob/living/carbon/human/gib_animation()
	new /obj/effect/temp_visual/gib_animation(loc, "gibbed-h")

/mob/living/carbon/human/dust_animation()
	new /obj/effect/temp_visual/dust_animation(loc, "dust-h")

/mob/living/carbon/human/spawn_gibs(with_bodyparts)
	if(with_bodyparts)
		new /obj/effect/gibspawner/human(drop_location(), src)
	else
		new /obj/effect/gibspawner/human/bodypartless(drop_location(), src)

/mob/living/carbon/human/spawn_dust(just_ash = FALSE)
	if(just_ash)
		for(var/i in 1 to 5)
			new /obj/item/ash(loc)
	else
		new /obj/effect/decal/remains/human(loc)

/proc/rogueviewers(range, object)
	. = list(viewers(range, object))
	if(isliving(object))
		var/mob/living/LI = object
		for(var/mob/living/L in .)
			if(!L.can_see_cone(LI))
				. -= L
			if(HAS_TRAIT(L, TRAIT_BLIND))
				. -= L

/mob/living/carbon/human/death(gibbed, nocutscene = FALSE)
	if(stat == DEAD)
		return

	var/area/A = get_area(src)
	dna?.species?.stop_wagging_tail(src)

	//OV edit
	if(isooze(src))
		var/obj/shapeshift_holder/ooze_death/H = locate() in src
		if(!H)
			var/shapeshift_type = /mob/living/simple_animal/hostile/retaliate/rogue/ooze_blob/suffering
			var/mob/living/shape = new shapeshift_type(src.loc)
			shape.color = "#[dna.features["mcolor"]]"

			H = new(shape,src)
			shape.name = "[shape]"

			shape.mind.RemoveSpell(/obj/effect/proc_holder/spell/targeted/shapeshift/ooze)

			return
	//OV edit end

	if(client)
		SSdroning.kill_droning(client)
		SSdroning.kill_loop(client)
		SSdroning.kill_rain(client)

	// Dusting deaths - IronDragoon
	if(!gibbed && HAS_TRAIT(src, TRAIT_DUSTABLE))
		if(HAS_TRAIT(src, TRAIT_DUST_LEAVE_HEAD))
			var/obj/item/bodypart/head/head = get_bodypart(BODY_ZONE_HEAD)
			if(head)
				head.drop_limb()
		var/delete_gear = HAS_TRAIT(src, TRAIT_DUST_DELETE_GEAR)
		if(delete_gear)
			for(var/obj/item/gear in get_equipped_items(TRUE) + held_items)
				qdel(gear)
		dust(just_ash=TRUE, drop_items=!delete_gear)
		return

	if(mind)
		if(!gibbed)
			var/has_secondlife = HAS_TRAIT(mind.current, TRAIT_SECONDLIFE)
			if(has_secondlife)
				var/respawn_time = 5 SECONDS
				var/datum/mind/playermind = mind
				addtimer(CALLBACK(src, PROC_REF(secondliferespawn), playermind), respawn_time, TIMER_UNIQUE)
				REMOVE_TRAIT(mind.current,TRAIT_SECONDLIFE,TRAIT_GENERIC)
		
		var/datum/antagonist/lich/L = mind.has_antag_datum(/datum/antagonist/lich)
		if (L && !L.out_of_lives)
			if(L.consume_phylactery())
				visible_message(span_warning("[src]'s body begins to shake violently, as eldritch forces begin to whisk them away!"))
				to_chat(src, span_userdanger("Death is not the end for me. I begin to rise again."))
				playsound(src, 'sound/magic/antimagic.ogg', 100, FALSE)
			else
				to_chat(src, span_userdanger("No, NO! This cannot be!"))
				L.out_of_lives = TRUE
				gib()
				return

	if(client || mind)
		record_round_statistic(STATS_DEATHS)
		var/area_of_death = LOWER_TEXT(get_area_name(src))
		if(area_of_death == "wilderness")
			record_round_statistic(STATS_FOREST_DEATHS)
		if(is_noble())
			record_round_statistic(STATS_NOBLE_DEATHS)
		if(ishumannorthern(src))
			record_round_statistic(STATS_HUMEN_DEATHS)
		if(mind)
			if(mind.assigned_role in GLOB.church_positions)
				record_round_statistic(STATS_CLERGY_DEATHS)
			if(mind.has_antag_datum(/datum/antagonist/vampire))
				record_round_statistic(STATS_VAMPIRES_KILLED)
			if(mind.has_antag_datum(/datum/antagonist/zombie))
				record_round_statistic(STATS_DEADITES_KILLED)
			if(mind.has_antag_datum(/datum/antagonist/skeleton) || mind.has_antag_datum(/datum/antagonist/lich))
				record_round_statistic(STATS_SKELETONS_KILLED)
	
	var/notreally = FALSE

	if(!gibbed)
		if(mind?.has_antag_datum(/datum/antagonist/vampire) && vampire_resurrect_chances) // You only have 1 chance by default. You gain 1 chance per person you frag through blood drinking.
			begin_vampire_torpor()
			vampire_resurrect_chances--
			notreally = TRUE
			to_chat(src, span_artery("<i>...Wryyyyy...</i>"))
			playsound(src, 'sound/vo/mobs/ghost/death.ogg', 15, FALSE, -1)
		else
			to_chat(src, span_artery("<i>Ashes, ashes.<br>They all.<br>Fall.<br>Down.</i>"))
			playsound(src, 'sound/magic/psydonmusicbox.ogg', 15)

	if(!gibbed)
		/*
			ZOMBIFICATION BY DEATH BEGINS HERE
		*/
		if(!has_world_trait(/datum/world_trait/necra_requiem))
			if(!is_in_roguetown(src) || has_world_trait(/datum/world_trait/zizo_defilement))
				if(!zombie_check_can_convert()) //Gives the dead unit the zombie antag flag
					to_chat(src, span_userdanger("..is this to be my end..?"))
					if(notreally)
						to_chat(src, span_artery("...No, it's not over yet... I'll be back. I'll always be back."))
					to_chat(src, span_danger("The cold consumes the final flicker of warmth in your chest and begins to seep into your limbs..."))

	stop_sound_channel(CHANNEL_HEARTBEAT)
	var/obj/item/organ/heart/H = getorganslot(ORGAN_SLOT_HEART)
	if(H)
		H.beat = BEAT_NONE

	if(!mob_timers["deathdied"])
		mob_timers["deathdied"] = world.time
		var/tris2take = 0
		if(istype(A, /area/rogue/indoors/town/cell))
			tris2take += -2
//		else
//			if(get_triumphs() > 0)
//				tris2take += -1
		if(H in SStreasury.bank_accounts)
			for(var/obj/structure/roguemachine/camera/C in view(7, src))
				var/area_name = A.name
				var/texty = "<CENTER><B>Death of a Living Being</B><br>---<br></CENTER>"
				texty += "[real_name] perished in front of face #[C.number] ([area_name]) at [station_time_timestamp("hh:mm")]."
				SSroguemachine.death_queue += texty
				break

		var/yeae = TRUE
		if(buckled)
			if(istype(buckled, /obj/structure/fluff/psycross))
				if(real_name in GLOB.excommunicated_players)
					yeae = FALSE
					tris2take += -2
				if(real_name in GLOB.outlawed_players)
					yeae = FALSE
/*
		if(get_triumphs() > 0)
			if(tris2take)
				adjust_triumphs(tris2take)
			else
				adjust_triumphs(-1)
*/
		switch(job)
			if("Grand Duke")
				//omen gets added separately, after a few minutes
				for(var/mob/living/carbon/human/HU in GLOB.player_list)
					if(!HU.stat && is_in_roguetown(HU))
						HU.playsound_local(get_turf(HU), 'sound/music/lorddeath.ogg', 80, FALSE, pressure_affected = FALSE)
			if("Bishop")
				addomen(OMEN_NOPRIEST)
//		if(yeae)
//			if(mind)
//				if((mind.assigned_role == "Lord") || (mind.assigned_role == "Priest") || (mind.assigned_role == "Knight Captain") || (mind.assigned_role == "Merchant"))
//					addomen(OMEN_NOBLEDEATH)

		if(!gibbed && yeae)
			for(var/mob/living/carbon/human/HU in viewers(7, src))
				if(HU.marriedto == src)
					HU.adjust_triumphs(-1)

	. = ..()

	if(isdullahan(src))
		var/datum/species/dullahan/user_species = src.dna.species
		if(user_species.headless)
			user_species.soul_light_off()
			update_body()

	dizziness = 0
	jitteriness = 0
	dna.species.spec_death(gibbed, src)

	if(isdullahan(src))
		var/datum/species/dullahan/user_species = src.dna.species
		if(user_species.headless)
			user_species.soul_light_off()
			update_body()

	if(SSticker.HasRoundStarted())
		SSblackbox.ReportDeath(src)
		log_message("has died (BRUTE: [src.getBruteLoss()], BURN: [src.getFireLoss()], TOX: [src.getToxLoss()], OXY: [src.getOxyLoss()], CLONE: [src.getCloneLoss()])", LOG_ATTACK)
		if(client || mind)
			var/death_admin_message = "[key_name(src)] [loc_name(src)] [ADMIN_FLW(src)] has died (BRUTE: [src.getBruteLoss()], BURN: [src.getFireLoss()], TOX: [src.getToxLoss()], OXY: [src.getOxyLoss()], CLONE: [src.getCloneLoss()])"
			message_admins(death_admin_message)
			log_admin(death_admin_message)

/mob/living/carbon/human/revive(full_heal, admin_revive)
	. = ..()
	if(!.)
		return
	switch(job)
		if("Grand Duke", "Grand Duchess")
			removeomen(OMEN_NOLORD)
		if("Bishop")
			removeomen(OMEN_NOPRIEST)

/mob/living/carbon/human/gib(no_brain, no_organs, no_bodyparts, safe_gib = FALSE)
	record_round_statistic(STATS_PEOPLE_GIBBED)
	for(var/mob/living/carbon/human/CA in viewers(7, src))
		if(CA != src && !HAS_TRAIT(CA, TRAIT_BLIND))
			if(HAS_TRAIT(CA, TRAIT_STEELHEARTED))
				continue
			if(CA.marriedto == src)
				CA.adjust_triumphs(-1)
			CA.add_stress(/datum/stressevent/viewgib)
	return ..()

/mob/living/carbon/human/proc/secondliferespawn(datum/mind/mind)
	var/mob_type = /mob/living/carbon/human
	var/turf/T = get_turf(src)
	var/mob/living/body

	//drop everything they had on the ground
	if(T)
		for(var/X in bodyparts)
			var/obj/item/bodypart/BP = X
			for(var/obj/item/I as anything in BP.embedded_objects)
				I.forceMove(T)

	if(mind.current)
		if(mind.current.stat != DEAD)
			return
		else
			body = mind.current
	if(!body)
		body = new mob_type(T)
		var/mob/ghostie = mind.get_ghost(TRUE)
		if(ghostie.client && ghostie.client.prefs)
			ghostie.client.prefs.copy_to(body)
		mind.transfer_to(body)
	else
		body.forceMove(pick(GLOB.secondlife_respawns))
		body.revive(full_heal = TRUE, admin_revive = TRUE)
	mind.grab_ghost(TRUE)
	body.flash_act()

	playsound(T, 'sound/magic/antimagic.ogg', 50, TRUE)

/mob/living/carbon/human/proc/begin_vampire_torpor()
	if(HAS_TRAIT(src, TRAIT_VAMPIRE_TORPOR))
		return

	ADD_TRAIT(src, TRAIT_VAMPIRE_TORPOR, TRAIT_GENERIC)

	vampire_revival_progress = 0
	vampire_time_of_death = world.time

	addtimer(CALLBACK(src, PROC_REF(vampire_torpor_tick)), 1 SECONDS)


/mob/living/carbon/human/proc/vampire_torpor_tick()
	if(QDELETED(src))
		return
	if(!get_bodypart(BODY_ZONE_CHEST))
		return
	if(stat != DEAD)
		return
	if(!HAS_TRAIT(src, TRAIT_VAMPIRE_TORPOR)) // Removing the Torpor trait during regen will also stop them from reviving. Intended for Inquisition doohickeys in the future.
		return

	var/progress_gain = 1 SECONDS

	// Decapitation functions to completely stop the process.
	var/obj/item/bodypart/head/H = get_bodypart(BODY_ZONE_HEAD)
	if(!H)
		progress_gain = 0

	// Direct sunlight should stop the process too.
	if(is_in_torpor_sunlight())
		progress_gain = 0
	// Coffins and graves accelerate the timer. A cross, however, will properly round-remove a vampire and ash them.
	if(progress_gain)
		if(istype(loc, /obj/structure/closet/crate/coffin))
			progress_gain *= 2
		else if(istype(loc, /obj/structure/closet/dirthole/closed))
			progress_gain *= 2

	vampire_revival_progress += progress_gain

	// Blood feeding begins after 1 minute, basically, if you're bleeding around a vampire, we can skip 1 second per blood trickle
	if(world.time >= vampire_time_of_death + 1 MINUTES)
		for(var/obj/effect/decal/cleanable/blood/B in view(3, src))
			qdel(B)
			vampire_revival_progress += 1 SECONDS
			break

	if(vampire_revival_progress >= vampire_revival_target)
		vampire_resurrect()
		return

	addtimer(CALLBACK(src, PROC_REF(vampire_torpor_tick)), 1 SECONDS)

/mob/living/carbon/human/proc/is_in_torpor_sunlight()
	if(GLOB.tod != "day")
		return FALSE

	var/turf/T = get_turf(src)
	if(!T)
		return FALSE

	if(!T.can_see_sky())
		return FALSE
		
	if(HAS_TRAIT(src, TRAIT_VAMPIRE_SPAWN_PROTECTION))
		return FALSE

	return TRUE

/mob/living/carbon/human/proc/vampire_resurrect()
	if(stat != DEAD)
		return

	visible_message(span_warning("[src]'s corpse suddenly jolts awake!"), span_userdanger("Death releases its grip upon me. I LYYYYYVE!!!"))
	// HE LYYYYYYVES!!!!
	playsound(src, 'sound/magic/antimagic.ogg', 100, FALSE)

	revive(full_heal = TRUE, admin_revive = TRUE)
	emote("cackle")

	apply_status_effect(/datum/status_effect/vampire_spawn_protection)
	REMOVE_TRAIT(src, TRAIT_VAMPIRE_TORPOR, TRAIT_GENERIC)

	vampire_revival_progress = 0
	vampire_time_of_death = 0
	bloodpool = 0
